<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" width="100%" height="100%"
	mouseUp="activeNode=null;"
	>
	<mx:ApplicationControlBar x="0" y="0" width="100%">
		<mx:Button label="Add Node" click="addNodeHandler()"/>
		<mx:Button label="TraceXmlRepr" click="traceXml()"/>
	</mx:ApplicationControlBar>

	<mx:Script>
		<![CDATA[
			import mx.core.Application;
			// a map and an array to hold the graph elements
			public var nodesMap:Dictionary = new Dictionary();
			public var bgCanvas:Canvas;
			public var activeNode:GraphNode;		// used as a reference to create new connections

			public function generateUniqueId():String
			{
				// find the first id that is free in the given dictionary
				var id:int = 1;
				while (nodesMap[id.toString()] != undefined)
					id++;
				return id.toString();
			}
			public function reset():void
			{
				nodesMap = new Dictionary();
				removeAllChildren();
				bgCanvas = new Canvas();
				addChild(bgCanvas);
			}
			public function reDrawConnections():void
			{
				bgCanvas.graphics.clear();
				bgCanvas.graphics.lineStyle(0);
				for each (var n:GraphNode in nodesMap)
					for each (var c:String in n.outputToNodes)
					{
						var x:int, y:int;
						x = n.x + n.width/2;
						y = n.y + n.height;
						bgCanvas.graphics.moveTo(x, y);
						x = GraphNode(nodesMap[c]).x + GraphNode(nodesMap[c]).width/2;
						y = GraphNode(nodesMap[c]).y;
						bgCanvas.graphics.lineTo(x, y);
					}
			}
			public function addNodeHandler():void
			{
				var gn:GraphNode = new GraphNode();
				gn.x = 300;
				gn.y = 100;
				gn.targetContainer = (Application.application as RAWI_ClientSideInterface).propertiesPanel;
				gn.nodeId = generateUniqueId();
				nodesMap[gn.nodeId] = gn;
				gn.propsContainer.initialize()
				
				addChild(gn);
			}
			public function traceXml():void
			{
				// TODO: this is a temporary test function - delete in final release 
				var trGraph:XMLNode = new XMLNode(XMLNodeType.ELEMENT_NODE, "transformationGraph");
				for each (var gn:GraphNode in nodesMap)
					trGraph.appendChild(gn.getXmlRepresentation());
				trace(trGraph);
			}
		]]>
	</mx:Script>
</mx:Canvas>
