<?xml version="1.0" encoding="utf-8"?>
<PackNodeBase xmlns="RawiComponents.TMComponents.*" xmlns:mx="http://www.adobe.com/2006/mxml" initialize="init()">
	<mx:Label x="10" y="3" text="{propsContainer.nodeName.text}" id="nodeName"/>
	<mx:Script>
		<![CDATA[
			public var propsLocalContainer:propsPTN;
			private function init():void
			{
				this.nodeType = "packTransformerNode";
				this.m_inputCenter = new Point(0, 2);
				this.m_outputCenter = new Point(0, 19);
				this.propsLocalContainer = new propsPTN(); 
				this.propsContainer = this.propsLocalContainer;
				this.clipContent = false;
				this.propsContainer.ownerOfProps = this;
				trace("init from packTransformerNode");
			}
			public override function getXmlRepresentation():XMLNode
			{
				var repr:XMLNode = new XMLNode(XMLNodeType.ELEMENT_NODE, this.nodeType);
				repr.attributes.id = this.nid;
				if (this.propsContainer.nodeName.text != "")
					repr.attributes.name = this.propsContainer.nodeName.text;
				repr.attributes.x = this.x;
				repr.attributes.y = this.y;
				if (this.propsLocalContainer.isJoiner.selected)
					repr.attributes.isJoiner = true;
				if (this.propsLocalContainer.createBatch.selected)
					repr.attributes.createBatch = true;
				if (this.propsLocalContainer.systemCommand.selected)
					repr.attributes.systemCommand = true;
				for each (var ref:String in inputFromNodes)
				{
					var tempNode:XMLNode = new XMLNode(XMLNodeType.ELEMENT_NODE, "input");
					tempNode.attributes.node = ref;
					repr.appendChild(tempNode);
				}
				for each (var reff:String in outputToNodes)
				{
					var tempNode2:XMLNode = new XMLNode(XMLNodeType.ELEMENT_NODE, "output");
					tempNode2.attributes.node = reff;
					repr.appendChild(tempNode2);
				}
				var specific:XMLNode = new XMLNode(XMLNodeType.ELEMENT_NODE, "command");
				specific.attributes.exec = this.propsLocalContainer.command.text;
				repr.appendChild(specific);
				return repr;
			}
			public override function drawObject():void
			{
				this.graphics.clear();
				
				var fillType:String = GradientType.RADIAL;
				var colors:Array = [0xC0C9CF, 0xffffff];
				var alphas:Array = [1, 0.7];
				var ratios:Array = [0x88, 0xFF];
				var matr:Matrix = new Matrix();
				matr.createGradientBox(20, 20, 0, -10, 0);
				var spreadMethod:String = SpreadMethod.PAD;

				// Draw the object
				if (this.parentCont.activeNode == this)
					this.graphics.lineStyle(2, 0x888888, 1);
				else
					this.graphics.lineStyle(0, 0xffffff, 1);
				if (isMultiNode)
				{
					matr.createGradientBox(20, 20, 0, -10 + 10, 0 - 10);
					this.graphics.beginGradientFill(fillType, colors, alphas, ratios, matr, spreadMethod);
					this.graphics.drawCircle(0 + 10, 10 - 10, 10);
					this.graphics.endFill();

					matr.createGradientBox(20, 20, 0, -10 + 5, 0 - 5);
					this.graphics.beginGradientFill(fillType, colors, alphas, ratios, matr, spreadMethod);
					this.graphics.drawCircle(0 + 5, 10 - 5, 10);
					this.graphics.endFill();
				}
				matr.createGradientBox(20, 20, 0, -10, 0);
				this.graphics.beginGradientFill(fillType, colors, alphas, ratios, matr, spreadMethod);
				this.graphics.drawCircle(0, 10, 10);
				this.graphics.endFill();
			}
			public override function get isJoiner():Boolean
			{
				return this.propsLocalContainer.isJoiner.selected;
			}
		]]>
	</mx:Script>
</PackNodeBase>
